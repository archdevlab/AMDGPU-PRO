#Maintainer: archdevlab <https://github.com/archdevlab>
#Credits: Janusz Lewandowski <lew21@xtreeme.org>
#Credits: David McFarland <corngood@gmail.com>
#Credits: Andrew Shark <ashark @at@ linuxcomp.ru>

#Upstream pages
#https://aur.archlinux.org/pkgbase/amdgpu-pro-installer
#https://github.com/Ashark/archlinux-amdgpu-pro
#https://repo.radeon.com/amdgpu

major=23.40
minor=1710631
ubuntu_ver=22.04
repo_folder_ver=6.0.1

pkgbase=opengl-amdgpu-pro
pkgname=(opengl-amdgpu-pro lib32-opengl-amdgpu-pro)
pkgver=6.0.1
pkgrel=1
arch=(x86_64)
url='https://repo.radeon.com/amdgpu'
license=(custom: multiple)
source=(progl
        progl.bash-completion
        https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libegl1-amdgpu-pro-oglp_${major}-${minor}.${ubuntu_ver}_i386.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libegl1-amdgpu-pro-oglp_${major}-${minor}.${ubuntu_ver}_amd64.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgl1-amdgpu-pro-oglp-dri_${major}-${minor}.${ubuntu_ver}_i386.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgl1-amdgpu-pro-oglp-dri_${major}-${minor}.${ubuntu_ver}_amd64.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgl1-amdgpu-pro-oglp-ext_${major}-${minor}.${ubuntu_ver}_amd64.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgl1-amdgpu-pro-oglp-gbm_${major}-${minor}.${ubuntu_ver}_amd64.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgl1-amdgpu-pro-oglp-glx_${major}-${minor}.${ubuntu_ver}_i386.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgl1-amdgpu-pro-oglp-glx_${major}-${minor}.${ubuntu_ver}_amd64.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgles1-amdgpu-pro-oglp_${major}-${minor}.${ubuntu_ver}_i386.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgles1-amdgpu-pro-oglp_${major}-${minor}.${ubuntu_ver}_amd64.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgles2-amdgpu-pro-oglp_${major}-${minor}.${ubuntu_ver}_i386.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgles2-amdgpu-pro-oglp_${major}-${minor}.${ubuntu_ver}_amd64.deb)

extract_deb(){
  local tmpdir="$(basename "${1%.deb}")"
  rm -Rf "$tmpdir"
  mkdir "$tmpdir"
  cd "$tmpdir"
  ar x "$1"
  tar -C "${pkgdir}" -xf data.tar.xz
}

move_libdir(){
  local deb_libdir="$1"
  local arch_libdir="$2"

  if [ -d "${pkgdir}/${deb_libdir}" ]; then
    if [ ! -d "${pkgdir}/${arch_libdir}" ]; then
      mkdir -p "${pkgdir}/${arch_libdir}"
    fi
    mv -t "${pkgdir}/${arch_libdir}/" "${pkgdir}/${deb_libdir}"/*
    find ${pkgdir} -type d -empty -delete
  fi
}

move_copyright(){
  find ${pkgdir}/usr/share/doc -name "changelog.Debian.gz" -delete
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  find ${pkgdir}/usr/share/doc -name "copyright" -exec mv {} ${pkgdir}/usr/share/licenses/${pkgname} \;
  find ${pkgdir}/usr/share/doc -type d -empty -delete
}

package_opengl-amdgpu-pro(){
  pkgdesc='AMDGPU Pro OpenGL driver'
  arch=(x86_64)
  license=(custom: AMDGPU-PRO EULA)
  provides=(opengl-driver libegl libgl libgles)
  depends=(libdrm libx11 libxcb libxdamage libxext libxfixes libxxf86vm)
  conflicts=(opengl-amdgpu-pro-21.20)
  backup=(etc/amd/amdapfxx.blb)

  extract_deb "${srcdir}"/libegl1-amdgpu-pro-oglp_${major}-${minor}.${ubuntu_ver}_amd64.deb
  extract_deb "${srcdir}"/libgl1-amdgpu-pro-oglp-dri_${major}-${minor}.${ubuntu_ver}_amd64.deb
  extract_deb "${srcdir}"/libgl1-amdgpu-pro-oglp-ext_${major}-${minor}.${ubuntu_ver}_amd64.deb
  extract_deb "${srcdir}"/libgl1-amdgpu-pro-oglp-gbm_${major}-${minor}.${ubuntu_ver}_amd64.deb
  extract_deb "${srcdir}"/libgl1-amdgpu-pro-oglp-glx_${major}-${minor}.${ubuntu_ver}_amd64.deb
  extract_deb "${srcdir}"/libgles1-amdgpu-pro-oglp_${major}-${minor}.${ubuntu_ver}_amd64.deb
  extract_deb "${srcdir}"/libgles2-amdgpu-pro-oglp_${major}-${minor}.${ubuntu_ver}_amd64.deb
  move_libdir "opt/amdgpu-pro/lib/x86_64-linux-gnu" "usr/lib"
  move_copyright

  # extra_commands:
  move_libdir "usr/lib/x86_64-linux-gnu" "usr/lib"
  move_libdir "opt/amdgpu-pro/lib/x86_64-linux-gnu" "usr/lib/amdgpu-pro"
  move_libdir "opt/amdgpu-pro/lib/xorg" "usr/lib/amdgpu-pro/xorg"
  move_libdir "opt/amdgpu/share/drirc.d" "usr/share/drirc.d"
  install -Dm755 "${srcdir}"/progl "${pkgdir}"/usr/bin/progl
  install -Dm644 "${srcdir}"/progl.bash-completion "${pkgdir}"/usr/share/bash-completion/completions/progl
  # For some reason, applications started with normal OpenGL (i.e. without ag pro) crashes at launch if this conf file is presented, so hide it for now, until I find out the reason of that.
  mv "${pkgdir}"/usr/share/drirc.d/10-amdgpu-pro.conf "${pkgdir}"/usr/share/drirc.d/10-amdgpu-pro.conf.hide
  # For some reason, libs no more moved to the pro folder. Do it manually.
  mv -v -t "${pkgdir}/usr/lib/amdgpu-pro" "${pkgdir}/usr/lib/lib"*

  # extra commands fix
  move_libdir "opt/amdgpu/lib/x86_64-linux-gnu/dri" "usr/lib/amdgpu"
  move_libdir "opt/amdgpu/lib/x86_64-linux-gnu/gbm" "usr/lib/amdgpu"
  # symlink to new directory
  rm -rf "${pkgdir}/usr/lib/dri/amdgpu_dri.so"
  ln -s /usr/lib/amdgpu/amdgpu_dri.so "${pkgdir}/usr/lib/dri/amdgpu_dri.so"
}

package_lib32-opengl-amdgpu-pro(){
  pkgdesc='AMDGPU Pro OpenGL driver (32-bit)'
  arch=(i686 x86_64)
  license=(custom: AMDGPU-PRO EULA)
  provides=(lib32-opengl-driver lib32-libegl lib32-libgl lib32-libgles)
  depends=(opengl-amdgpu-pro=${major}_${minor}-${pkgrel} lib32-libdrm lib32-libx11 lib32-libxcb lib32-libxdamage lib32-libxext lib32-libxfixes lib32-libxxf86vm)
  conflicts=(lib32-opengl-amdgpu-pro-21.20)
  backup=(etc/amd/amdrc etc/ld.so.conf.d/10-amdgpu-pro-i386.conf)

  extract_deb "${srcdir}"/libegl1-amdgpu-pro-oglp_${major}-${minor}.${ubuntu_ver}_i386.deb
  extract_deb "${srcdir}"/libgl1-amdgpu-pro-oglp-dri_${major}-${minor}.${ubuntu_ver}_i386.deb
  extract_deb "${srcdir}"/libgl1-amdgpu-pro-oglp-glx_${major}-${minor}.${ubuntu_ver}_i386.deb
  extract_deb "${srcdir}"/libgles1-amdgpu-pro-oglp_${major}-${minor}.${ubuntu_ver}_i386.deb
  extract_deb "${srcdir}"/libgles2-amdgpu-pro-oglp_${major}-${minor}.${ubuntu_ver}_i386.deb
  move_libdir "opt/amdgpu-pro/lib/i386-linux-gnu" "usr/lib32"
  move_copyright

  # extra_commands:
  rm "${pkgdir}"/opt/amdgpu/share/drirc.d/10-amdgpu-pro.conf
  move_libdir "usr/lib/i386-linux-gnu" "usr/lib32"
  move_libdir "opt/amdgpu-pro/lib/i386-linux-gnu" "usr/lib32/amdgpu-pro"
  # For some reason, libs no more moved to the pro folder. Do it manually.
  mkdir "${pkgdir}/usr/lib32/amdgpu-pro"
  mv -v -t "${pkgdir}/usr/lib32/amdgpu-pro" "${pkgdir}/usr/lib32/lib"*

  # extra commands fix
  move_libdir "opt/amdgpu/lib/i386-linux-gnu/dri" "usr/lib32/amdgpu"
  # symlink to new directory
  rm -rf "${pkgdir}/usr/lib32/dri/amdgpu_dri.so"
  ln -s /usr/lib32/amdgpu/amdgpu_dri.so "${pkgdir}/usr/lib32/dri/amdgpu_dri.so"
}

sha256sums=('feb74796c3152cbafaba89d96e68a152f209bd3058c7eb0413cbe1ab0764e96f'
            'e32801c38b475cd8df17a407726b86db3de26410f563d688325b4d4314fc5354'
            '12717a49746fa62d2b5fdfc85d30a26038b0e5f52f5985ced5139dacc4659e73'
            '1d87e8f385c8c308691d918274e32862f80c09023069194d7b8172ace54cadb8'
            '95a66d0dffb2be645cf991dfc60fdc18cc3870629953606c89878870c2bc5658'
            '945f8d31560b98a34074fb10163aa8970e8da5f7dd81c53f5380d51c799981d4'
            'eb379cb64365e3437025702c8d1661d6e2c652ccd46a08ece96d8fb216d18f93'
            'd7a44fcdca5d3410318713d2a3c2cca33d1e399eaf7fb8d3eb58b69851f90111'
            'f0235039ce37e340ccc8c552fd9ac26bd0c6e74b3dc6222bf9e4813a9e7ff524'
            'e9d84a5ef9ed7e1885de51e7d9fe2a32f1635896628daaa1c0fcb83057cd5515'
            '5bbcedbc7ab1f37792e6e70f52c6910a74983bef6d53f4c47dc3a4813e89b01c'
            'cf1e748dd328e1900bd78c4c9dc140dcb2517fb1848a2cafd585c9782eb4dea8'
            'ba71cc6055a0133ed245bd818c72682f11f4641c6ee6b9a3627fcb808d16ac75'
            '076eeefef404d5e1f801f12ecf2abed727b0fcd15728133ca3623052f499ffd2')
