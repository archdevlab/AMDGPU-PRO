# _     _            _        _          _____
#| |__ | | __ _  ___| | _____| | ___   _|___ /
#| '_ \| |/ _` |/ __| |/ / __| |/ / | | | |_ \
#| |_) | | (_| | (__|   <\__ \   <| |_| |___) |
#|_.__/|_|\__,_|\___|_|\_\___/_|\_\\__, |____/
#                                  |___/

#Maintainer: blacksky3 <https://github.com/blacksky3>
#Credits: Janusz Lewandowski <lew21@xtreeme.org>
#Credits: David McFarland <corngood@gmail.com>
#Credits: Andrew Shark <ashark @at@ linuxcomp.ru>


#Upstream pages
#https://aur.archlinux.org/pkgbase/amdgpu-pro-installer
#https://github.com/Ashark/archlinux-amdgpu-pro
#https://repo.radeon.com/amdgpu

major=23.10
major_short=23.10
minor=1595145
ubuntu_ver=22.04
repo_folder_ver=5.5.1

pkgbase=opengl-amdgpu-pro
pkgname=(opengl-amdgpu-pro lib32-opengl-amdgpu-pro)
pkgver=${major}_${minor}
pkgrel=1
arch=(x86_64)
url='https://repo.radeon.com/amdgpu'
license=(custom: multiple)
source=(progl
        progl.bash-completion
        https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libegl1-amdgpu-pro-oglp_${major_short}-${minor}.${ubuntu_ver}_i386.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libegl1-amdgpu-pro-oglp_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgl1-amdgpu-pro-oglp-dri_${major_short}-${minor}.${ubuntu_ver}_i386.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgl1-amdgpu-pro-oglp-dri_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgl1-amdgpu-pro-oglp-ext_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgl1-amdgpu-pro-oglp-gbm_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgl1-amdgpu-pro-oglp-glx_${major_short}-${minor}.${ubuntu_ver}_i386.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgl1-amdgpu-pro-oglp-glx_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgles1-amdgpu-pro-oglp_${major_short}-${minor}.${ubuntu_ver}_i386.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgles1-amdgpu-pro-oglp_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgles2-amdgpu-pro-oglp_${major_short}-${minor}.${ubuntu_ver}_i386.deb
      	https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/oglp-amdgpu-pro/libgles2-amdgpu-pro-oglp_${major_short}-${minor}.${ubuntu_ver}_amd64.deb)

extract_deb(){
  local tmpdir="$(basename "${1%.deb}")"
  rm -Rf "$tmpdir"
  mkdir "$tmpdir"
  cd "$tmpdir"
  ar x "$1"
  tar -C "${pkgdir}" -xf data.tar.xz
}

move_libdir(){
  local deb_libdir="$1"
  local arch_libdir="$2"

  if [ -d "${pkgdir}/${deb_libdir}" ]; then
    if [ ! -d "${pkgdir}/${arch_libdir}" ]; then
      mkdir -p "${pkgdir}/${arch_libdir}"
    fi
    mv -t "${pkgdir}/${arch_libdir}/" "${pkgdir}/${deb_libdir}"/*
    find ${pkgdir} -type d -empty -delete
  fi
}

move_copyright(){
  find ${pkgdir}/usr/share/doc -name "changelog.Debian.gz" -delete
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  find ${pkgdir}/usr/share/doc -name "copyright" -exec mv {} ${pkgdir}/usr/share/licenses/${pkgname} \;
  find ${pkgdir}/usr/share/doc -type d -empty -delete
}

package_opengl-amdgpu-pro(){
  pkgdesc='AMDGPU Pro OpenGL driver'
  arch=(x86_64)
  license=(custom: AMDGPU-PRO EULA)
  provides=(opengl-driver libegl libgl libgles)
  depends=(libdrm libx11 libxcb libxdamage libxext libxfixes libxxf86vm)
  conflicts=(opengl-amdgpu-pro-21.20)
  backup=(etc/amd/amdapfxx.blb)

  extract_deb "${srcdir}"/libegl1-amdgpu-pro-oglp_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
  extract_deb "${srcdir}"/libgl1-amdgpu-pro-oglp-dri_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
  extract_deb "${srcdir}"/libgl1-amdgpu-pro-oglp-ext_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
  extract_deb "${srcdir}"/libgl1-amdgpu-pro-oglp-gbm_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
  extract_deb "${srcdir}"/libgl1-amdgpu-pro-oglp-glx_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
  extract_deb "${srcdir}"/libgles1-amdgpu-pro-oglp_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
  extract_deb "${srcdir}"/libgles2-amdgpu-pro-oglp_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
  move_libdir "opt/amdgpu-pro/lib/x86_64-linux-gnu" "usr/lib"
  move_copyright

  # extra_commands:
  move_libdir "usr/lib/x86_64-linux-gnu" "usr/lib"
  move_libdir "opt/amdgpu-pro/lib/x86_64-linux-gnu" "usr/lib/amdgpu-pro"
  move_libdir "opt/amdgpu-pro/lib/xorg" "usr/lib/amdgpu-pro/xorg"
  move_libdir "opt/amdgpu/share/drirc.d" "usr/share/drirc.d"
  install -Dm755 "${srcdir}"/progl "${pkgdir}"/usr/bin/progl
  install -Dm644 "${srcdir}"/progl.bash-completion "${pkgdir}"/usr/share/bash-completion/completions/progl
  # For some reason, applications started with normal OpenGL (i.e. without ag pro) crashes at launch if this conf file is presented, so hide it for now, until I find out the reason of that.
  mv "${pkgdir}"/usr/share/drirc.d/10-amdgpu-pro.conf "${pkgdir}"/usr/share/drirc.d/10-amdgpu-pro.conf.hide
  # For some reason, libs no more moved to the pro folder. Do it manually.
  mv -v -t "${pkgdir}/usr/lib/amdgpu-pro" "${pkgdir}/usr/lib/lib"*

  # extra commands fix
  move_libdir "opt/amdgpu/lib/x86_64-linux-gnu/dri" "usr/lib/amdgpu"
  move_libdir "opt/amdgpu/lib/x86_64-linux-gnu/gbm" "usr/lib/amdgpu"
  # symlink to new directory
  rm -rf "${pkgdir}/usr/lib/dri/amdgpu_dri.so"
  ln -s /usr/lib/amdgpu/amdgpu_dri.so "${pkgdir}/usr/lib/dri/amdgpu_dri.so"
}

package_lib32-opengl-amdgpu-pro(){
  pkgdesc='AMDGPU Pro OpenGL driver (32-bit)'
  arch=(i686 x86_64)
  license=(custom: AMDGPU-PRO EULA)
  provides=(lib32-opengl-driver lib32-libegl lib32-libgl lib32-libgles)
  depends=(opengl-amdgpu-pro=${major}_${minor}-${pkgrel} lib32-libdrm lib32-libx11 lib32-libxcb lib32-libxdamage lib32-libxext lib32-libxfixes lib32-libxxf86vm)
  conflicts=(lib32-opengl-amdgpu-pro-21.20)
  backup=(etc/amd/amdrc etc/ld.so.conf.d/10-amdgpu-pro-i386.conf)

  extract_deb "${srcdir}"/libegl1-amdgpu-pro-oglp_${major_short}-${minor}.${ubuntu_ver}_i386.deb
  extract_deb "${srcdir}"/libgl1-amdgpu-pro-oglp-dri_${major_short}-${minor}.${ubuntu_ver}_i386.deb
  extract_deb "${srcdir}"/libgl1-amdgpu-pro-oglp-glx_${major_short}-${minor}.${ubuntu_ver}_i386.deb
  extract_deb "${srcdir}"/libgles1-amdgpu-pro-oglp_${major_short}-${minor}.${ubuntu_ver}_i386.deb
  extract_deb "${srcdir}"/libgles2-amdgpu-pro-oglp_${major_short}-${minor}.${ubuntu_ver}_i386.deb
  move_libdir "opt/amdgpu-pro/lib/i386-linux-gnu" "usr/lib32"
  move_copyright

  # extra_commands:
  rm "${pkgdir}"/opt/amdgpu/share/drirc.d/10-amdgpu-pro.conf
  move_libdir "usr/lib/i386-linux-gnu" "usr/lib32"
  move_libdir "opt/amdgpu-pro/lib/i386-linux-gnu" "usr/lib32/amdgpu-pro"
  # For some reason, libs no more moved to the pro folder. Do it manually.
  mkdir "${pkgdir}/usr/lib32/amdgpu-pro"
  mv -v -t "${pkgdir}/usr/lib32/amdgpu-pro" "${pkgdir}/usr/lib32/lib"*

  # extra commands fix
  move_libdir "opt/amdgpu/lib/i386-linux-gnu/dri" "usr/lib32/amdgpu"
  # symlink to new directory
  rm -rf "${pkgdir}/usr/lib32/dri/amdgpu_dri.so"
  ln -s /usr/lib32/amdgpu/amdgpu_dri.so "${pkgdir}/usr/lib32/dri/amdgpu_dri.so"
}

sha256sums=('feb74796c3152cbafaba89d96e68a152f209bd3058c7eb0413cbe1ab0764e96f'
            'e32801c38b475cd8df17a407726b86db3de26410f563d688325b4d4314fc5354'
            '8961a5a19a5b8c84d54df6be125f494fe238aa28267ff1778c9e4e5384281182'
            '3533c597e6e16d3e2d25726c51f1bad1b7ee8f083e72b13b6365ba0807f44a6c'
            'd633fb5c54956a8247cffe9f69f9ad57fa2aaaf4c273f8b1fdca4717ea2c41c4'
            'e694fdd17c0abf6aa4bd11737d97eab0792c611fcab3a283dc9740ca18fdc725'
            '61def861b132d76d5a9ac43c3f13a39d0206a3f409ccd450c4788351ef5c9777'
            '13e4f5f77fbf43a9dd1c0ab595ff233ecc45299ff4489c10fd70498e0555058b'
            '114a225282cf6f7e2a1d0ab84f716ca6a93dac6c2cc186cd3f61aa32a5061af0'
            '5ba1faceb840ccc0cd235e01efc54b984028520083ba41ef0a4392acb56da81e'
            'ab3f9016b66940f6cc04544da8c576ca2d5d7482da570637b40c0a0d0be1365e'
            '9c0b42db7a0cd0372cf7ca43b1ddfa75d0a0b9584d7ea22c964bd3a2f1b3182d'
            '5cb48db10863235c56313bc18a0867d8ee1ca04d46b5ff072c8fdbca586eb395'
            '4754d1663625fd93d1dafb88c5ecd2b22bc3a818cdfbeec865c4e61c3bc750e5')
