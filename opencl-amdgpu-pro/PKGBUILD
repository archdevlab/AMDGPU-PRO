#Maintainer: archdevlab <https://github.com/archdevlab>

#Upstream pages
#https://aur.archlinux.org/packages/opencl-legacy-amdgpu-pro
#https://repo.radeon.com/amdgpu

major=23.20
major_short=23.20
minor=1654522
ubuntu_ver=22.04
repo_folder_ver=5.7

pkgbase=opencl-amdgpu-pro
pkgname=(opencl-amdgpu-pro lib32-opencl-amdgpu-pro)
pkgver=${major}_${minor}
pkgrel=1
arch=(x86_64)
url='https://repo.radeon.com/amdgpu'
license=(custom: multiple)
makedepends=(wget)
source=(https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/opencl-legacy-amdgpu-pro/opencl-legacy-amdgpu-pro-icd_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
        https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/opencl-legacy-amdgpu-pro/opencl-legacy-amdgpu-pro-icd_${major_short}-${minor}.${ubuntu_ver}_i386.deb)

extract_deb(){
  local tmpdir="$(basename "${1%.deb}")"
  rm -Rf "$tmpdir"
  mkdir "$tmpdir"
  cd "$tmpdir"
  ar x "$1"
  tar -C "${pkgdir}" -xf data.tar.xz
}

move_libdir(){
  local deb_libdir="$1"
  local arch_libdir="$2"

  if [ -d "${pkgdir}/${deb_libdir}" ]; then
    if [ ! -d "${pkgdir}/${arch_libdir}" ]; then
      mkdir -p "${pkgdir}/${arch_libdir}"
    fi
    mv -t "${pkgdir}/${arch_libdir}/" "${pkgdir}/${deb_libdir}"/*
    find ${pkgdir} -type d -empty -delete
  fi
}

move_copyright(){
  find ${pkgdir}/usr/share/doc -name "changelog.Debian.gz" -delete
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  find ${pkgdir}/usr/share/doc -name "copyright" -exec mv {} ${pkgdir}/usr/share/licenses/${pkgname} \;
  find ${pkgdir}/usr/share/doc -type d -empty -delete
}

package_opencl-amdgpu-pro(){
  pkgdesc='Non-free AMD OpenCL ICD Loaders (PAL)'
  arch=(x86_64)
  license=(custom: AMDGPU-PRO EULA)
  conflicts=(opencl-amd opencl-amdgpu-pro-21.20)
  provides=(opencl-driver)
  optdepends=(clinfo rocm-opencl-runtime)

  extract_deb "${srcdir}"/opencl-legacy-amdgpu-pro-icd_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
  move_libdir "opt/amdgpu-pro/lib/x86_64-linux-gnu" "usr/lib"
  move_copyright
  touch ${pkgdir}/etc/OpenCL/vendors/amdocl-orca.icd
  echo libamdocl-orca.so >> ${pkgdir}/etc/OpenCL/vendors/amdocl-orca.icd
  touch ${pkgdir}/etc/OpenCL/vendors/amdocl-orca-ld-64.icd
  echo /usr/lib/libamdocl-orca64.so >> ${pkgdir}/etc/OpenCL/vendors/amdocl-orca-ld-64.icd
  ln -s /usr/lib/libamdocl-orca64.so ${pkgdir}/usr/lib/libamdocl-orca.so
}

package_lib32-opencl-amdgpu-pro(){
  pkgdesc='Non-free AMD OpenCL ICD Loaders (PAL) (32-bit)'
  arch=(i686 x86_64)
  license=(custom: AMDGPU-PRO EULA)
  depends=(opencl-amdgpu-pro=${major}_${minor}-${pkgrel})
  conflicts=(opencl-amd lib32-opencl-amdgpu-pro-21.20)
  provides=(lib32-opencl-driver)

  extract_deb "${srcdir}"/opencl-legacy-amdgpu-pro-icd_${major_short}-${minor}.${ubuntu_ver}_i386.deb
  move_libdir "opt/amdgpu-pro/lib/i386-linux-gnu" "usr/lib32"
  move_copyright
  touch ${pkgdir}/etc/OpenCL/vendors/amdocl-orca-ld-32.icd
  echo /usr/lib32/libamdocl-orca32.so >> ${pkgdir}/etc/OpenCL/vendors/amdocl-orca-ld-32.icd
  ln -s /usr/lib32/libamdocl-orca32.so ${pkgdir}/usr/lib32/libamdocl-orca.so
}

sha256sums=('bace2ef86be1dc823894e242e5077a70898062cc5b4db40962c19ca5bb6cf234'
            'b643a9ecbb8bea839fbcf4c02bf77e1250a878968ea0c9a47a045e46ea26a222')
