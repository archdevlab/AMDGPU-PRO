#Maintainer: archdevlab <https://github.com/archdevlab>

#Upstream pages
#https://repo.radeon.com/amdgpu

major=23.20
major_short=23.20
minor=1654522
ubuntu_ver=22.04
repo_folder_ver=5.7

pkgbase=ocl-icd-amdgpu-pro
pkgname=(ocl-icd-amdgpu-pro lib32-ocl-icd-amdgpu-pro)
pkgver=${major}_${minor}
pkgrel=1
arch=(x86_64)
url='https://repo.radeon.com/amdgpu'
license=(custom: multiple)
makedepends=(wget)
source=(https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/ocl-icd-amdgpu-pro/ocl-icd-libopencl1-amdgpu-pro_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
        https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/ocl-icd-amdgpu-pro/ocl-icd-libopencl1-amdgpu-pro_${major_short}-${minor}.${ubuntu_ver}_i386.deb
        https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/ocl-icd-amdgpu-pro/ocl-icd-libopencl1-amdgpu-pro-dev_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
        https://repo.radeon.com/amdgpu/${repo_folder_ver}/ubuntu/pool/proprietary/o/ocl-icd-amdgpu-pro/ocl-icd-libopencl1-amdgpu-pro-dev_${major_short}-${minor}.${ubuntu_ver}_i386.deb)

extract_deb(){
  local tmpdir="$(basename "${1%.deb}")"
  rm -Rf "$tmpdir"
  mkdir "$tmpdir"
  cd "$tmpdir"
  ar x "$1"
  tar -C "${pkgdir}" -xf data.tar.xz
}

move_libdir(){
  local deb_libdir="$1"
  local arch_libdir="$2"

  if [ -d "${pkgdir}/${deb_libdir}" ]; then
    if [ ! -d "${pkgdir}/${arch_libdir}" ]; then
      mkdir -p "${pkgdir}/${arch_libdir}"
    fi
    mv -t "${pkgdir}/${arch_libdir}/" "${pkgdir}/${deb_libdir}"/*
    find ${pkgdir} -type d -empty -delete
  fi
}

move_copyright(){
  find ${pkgdir}/usr/share/doc -name "changelog.Debian.gz" -delete
  mkdir -p ${pkgdir}/usr/share/licenses/${pkgname}
  find ${pkgdir}/usr/share/doc -name "copyright" -exec mv {} ${pkgdir}/usr/share/licenses/${pkgname} \;
  find ${pkgdir}/usr/share/doc -type d -empty -delete
}

package_ocl-icd-amdgpu-pro(){
  pkgdesc='AMD OpenCL ICD Loader library'
  arch=(x86_64)
  depends=(glibc)
  optdepends=('opencl-driver: packaged opencl driver')
  conflicts=(libcl ocl-icd ocl-icd-git khronos-ocl-icd-git ocl-icd-amdgpu-pro-21.20)
  replaces=(libcl)
  provides=(opencl-icd-loader ocl-icd ocl-icd-amdgpu-pro)

  extract_deb "${srcdir}"/ocl-icd-libopencl1-amdgpu-pro_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
  extract_deb "${srcdir}"/ocl-icd-libopencl1-amdgpu-pro-dev_${major_short}-${minor}.${ubuntu_ver}_amd64.deb
  move_libdir "opt/amdgpu-pro/lib/x86_64-linux-gnu" "usr/lib"
  move_copyright

  sed -i "s#prefix=/opt/amdgpu-pro#prefix=/usr#g" "${pkgdir}"/usr/lib/pkgconfig/OpenCL.pc
  sed -i "s#libdir=\${prefix}/lib/x86_64-linux-gnu#libdir=\${exec_prefix}/lib#g" "${pkgdir}"/usr/lib/pkgconfig/OpenCL.pc
  sed -i "s#L/opt/amdgpu-pro/lib/x86_64-linux-gnu#L\${libdir}#g" "${pkgdir}"/usr/lib/pkgconfig/OpenCL.pc
  sed -i "s#-I/opt/amdgpu-pro/include#-I\${includedir}#g" "${pkgdir}"/usr/lib/pkgconfig/OpenCL.pc
}

package_lib32-ocl-icd-amdgpu-pro(){
  pkgdesc='AMD OpenCL ICD Loader library (32-bit)'
  arch=(i686 x86_64)
  depends=(lib32-glibc ocl-icd-amdgpu-pro=${major}_${minor}-${pkgrel})
  optdepends=('lib32-opencl-driver: packaged opencl driver')
  conflicts=(lib32-libcl lib32-ocl-icd lib32-ocl-icd-git lib32-khronos-ocl-icd-git lib32-ocl-icd-amdgpu-pro-21.20)
  replaces=(lib32-libcl)
  provides=(lib32-opencl-icd-loader lib32-ocl-icd lib32-ocl-icd-amdgpu-pro)

  extract_deb "${srcdir}"/ocl-icd-libopencl1-amdgpu-pro_${major_short}-${minor}.${ubuntu_ver}_i386.deb
  extract_deb "${srcdir}"/ocl-icd-libopencl1-amdgpu-pro-dev_${major_short}-${minor}.${ubuntu_ver}_i386.deb
  move_libdir "opt/amdgpu-pro/lib/i386-linux-gnu" "usr/lib32"
  move_copyright

  sed -i "s#prefix=/opt/amdgpu-pro#prefix=/usr#g" "${pkgdir}"/usr/lib32/pkgconfig/OpenCL.pc
  sed -i "s#libdir=\${prefix}/lib/i386-linux-gnu#libdir=\${exec_prefix}/lib32#g" "${pkgdir}"/usr/lib32/pkgconfig/OpenCL.pc
  sed -i "s#L/opt/amdgpu-pro/lib/i386-linux-gnu#L\${libdir}#g" "${pkgdir}"/usr/lib32/pkgconfig/OpenCL.pc
  sed -i "s#-I/opt/amdgpu-pro/include#-I\${includedir}#g" "${pkgdir}"/usr/lib32/pkgconfig/OpenCL.pc
}

sha256sums=('8377548e754548d859163824a10257a101612bb744ddb551f5241486145aa298'
            'c5da97d5a3b50fc32745cb880f5244c54fd69f9572b7d43f6ede1178224a4d5f'
            '7c9186d7ef0526684104c1df07f143bb797b7c0c1a6ab7f49b07c63746fe0bdc'
            '859c827b5b684407e7d234cef18e5c7ab8bad3068c7a29e8a0f88c8ae312f679')
